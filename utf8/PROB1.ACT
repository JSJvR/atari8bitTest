BYTE FUNC GetNumber(BYTE ARRAY val)
  BYTE first,last,i,len
  first=0
  last=0

  len=val(0)
  i=1

  WHILE i<= len AND (val(i)<'0 OR val(i)>'9)
  DO
    i==+1
  OD
  first = val(i)-'0
  last = val(i)-'0
              
  ; If no valid digits were found, return 0          
  IF first>=10 THEN
    RETURN (0)
  FI

  WHILE i<= len
  DO
    IF val(i)>='0 AND val(i)<='9 THEN
      last=val(i)-'0
    FI
             
    i==+1
  OD
 
  ;PrintBE(first)
  ;PrintBE(last)           
  ;PrintE("")

RETURN (first*10+last)

PROC Main()
  BYTE ARRAY buf(255)
  CARD numLines = [0]
  BYTE num
  CARD total = [0]

  Open(1,"D:INPUT01.TXT",4,0)
          
  DO           
    InputSD(1,buf)  
    numLines==+1
   ; PrintE(buf)
    num = GetNumber(buf)
    PrintBE(num)
    total==+num
  UNTIL buf(0)=0 OR numLines>1500
  OD
    
  PrintCE(numLines)
  PrintCE(total)
  Close(1)
  
RETURN

